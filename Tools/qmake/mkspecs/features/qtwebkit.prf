# -------------------------------------------------------------------
# This file is used by tools that rely on the QtWebKit library
#
# It is only used for Qt4, which does not have a module system.
#
# See 'Tools/qmake/README' for an overview of the build system
# -------------------------------------------------------------------

haveQt(4) {
    QMAKE_LIBDIR = $$ROOT_BUILD_DIR/lib $$QMAKE_LIBDIR
    QTWEBKITLIBNAME = QtWebKit
    mac:!static:contains(QT_CONFIG, qt_framework):!CONFIG(webkit_no_framework) {
        LIBS += -framework $$QTWEBKITLIBNAME
        QMAKE_FRAMEWORKPATH = $$ROOT_BUILD_DIR/lib $$QMAKE_FRAMEWORKPATH
    } else {
        build_pass: win32-*|wince* {
            !CONFIG(release, debug|release): QTWEBKITLIBNAME = $${QTWEBKITLIBNAME}d
            QTWEBKITLIBNAME = $${QTWEBKITLIBNAME}$${QT_MAJOR_VERSION}
            win32-g++*: LIBS += -l$$QTWEBKITLIBNAME
            else: LIBS += $${QTWEBKITLIBNAME}.lib
        } else {
            LIBS += -lQtWebKit
        }
    }

    DEPENDPATH += $$ROOT_WEBKIT_DIR/Source/WebKit/qt/Api
    INCLUDEPATH += $${ROOT_BUILD_DIR}/include/QtWebKit
} else {
    error(Relying on $$_FILE_ for Qt5 builds are not supported)
}
