# -------------------------------------------------------------------
# This file ensures that QtUiTools is available before including the
# feature.
#
# See 'Tools/qmake/README' for an overview of the build system
# -------------------------------------------------------------------

# Disable by default
CONFIG -= uitools

haveQt(5) {
    !isEmpty(QT.uitools.name): CONFIG += uitools
} else {
    # 4.x
    libraryName = $$fromfile($$[QT_INSTALL_LIBS]/libQtUiTools.prl, QMAKE_PRL_TARGET)
    !isEmpty(libraryName):exists($$[QT_INSTALL_LIBS]/$$libraryName): CONFIG += uitools
}

CONFIG(uitools):load(uitools)
else {
    !build_pass: message("QtUiTools library not found. QWidget plugin loading will be disabled")
    DEFINES *= QT_NO_UITOOLS
}
